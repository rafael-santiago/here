#                       Copyright (C) 2014 by Rafael Santiago
#
# Here's main Forgefile.
#
include ~/toolsets/gcc/gcc-lib.hsl
include ~/toolsets/clang/clang-lib.hsl
include ~/toolsets/common/utils/lang/c/dependency_scanner.hsl
include Forgeutils.hsl

var deps type string;
var sources type list;
var includes type list;
var cflags type list;
var libraries type list;
var ldflags type list;

var chosen_toolset type string;

project here : toolset $chosen_toolset : dependencies $deps : $sources,
                                                              $includes,
                                                              $cflags,
                                                              $libraries,
                                                              $ldflags,
                                                              "libhere.a" ;

here.preloading() {
    $chosen_toolset = get_toolset_basename() + "lib";
}

here.prologue() {
    $deps = get_c_cpp_deps();
    $sources.ls(".*\\.c$");
}

here.epilogue() {
    var notest type list;
    var exit_code type int;
    if (hefesto.sys.last_forge_result() == 0) {
        $notest = hefesto.sys.get_option("no-test");
        if ($notest.count() == 0) {
            $exit_code = run_tests();
        }
    }
}

function run_tests() : result type int {
    var retval type int;
    hefesto.sys.cd("test");
    $retval = hefesto.sys.forge("here-test", "Forgefile.hsl", "--bin-output-dir=bin --obj-output-dir=obj");
    hefesto.sys.cd("..");
    result $retval;
}
